AppType=JavaFX
Build1=Default,b4j.example
File1=background.jpg
File10=oneCategory.bjl
File11=paginator.bjl
File12=skeleton-all.min.css
File13=styles.css
File2=FontAwesome.otf
File3=fontawesome-webfont.eot
File4=fontawesome-webfont.svg
File5=fontawesome-webfont.ttf
File6=fontawesome-webfont.woff
File7=fontawesome-webfont.woff2
File8=layout1.bjl
File9=MultiLayout.bjl
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup11=Default Group
FileGroup12=Default Group
FileGroup13=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=banano
Library4=bananoskeleton
Library5=jshell
Module1=HTTPRequests
NumberOfFiles=13
NumberOfLibraries=5
NumberOfModules=1
Version=7.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
	#IgnoreWarnings: 16, 10, 14, 15
#End Region

Sub Process_Globals
	Private BANano As BANano 'ignore
	Private Keyword As SKTextBox 'ignore
	Private SearchButton As SKButton 'ignore
	Private SKNavigationBar1 As SKNavigationBar 'ignore
	Private ResultList As SKList
	Private currentPageNum As Int=1
	Private querySize As Int=15
	Private id As String
	Private ResultRow As SKRow
	Private CategoryRow As SKRow
	Private categoryNum As Int
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	
	' you can change some output params here
	BANano.Initialize("BANano", "Search",DateTime.Now)
	BANano.HTML_NAME = "search.html"
	BANano.JAVASCRIPT_NAME = "searchpage" & DateTime.Now & ".js"
	BANano.SERVICEWORKER_NAME="searchSW.js"
	BANano.MANIFEST_NAME="searchManifest.json"
	BANano.Header.Title="拟声词检索"
	
	BANano.ExternalTestConnectionServer = "http://www.baidu.com"

	BANano.Header.AddCSSFile("skeleton-all.min.css")
	BANano.Header.AddCSSFile("styles.css")
	' start the build
	BANano.Build(File.DirApp)
	Log("copying...")
	Dim sh As Shell
	sh.Initialize("","xcopy.exe",Array("/E/Y","E:\B4J\SoundEffects\frontend\Search\Objects\Search\*","E:\B4J\SoundEffects\Objects\www"))
	sh.RunSynchronous(5000)
	Log("done")
	ExitApplication
End Sub

'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
	Return True
End Sub

' HERE STARTS YOUR APP
Sub BANano_Ready()
	' normal layouts
	BANano.LoadLayout("#body", "layout1")
	
	' Add some menu items (are added in reverse!)
	SKNavigationBar1.AddMenu("about","关于")
	SKNavigationBar1.AddMenu("upload","上传")
	SKNavigationBar1.AddMenu("logo", "拟声词检索")
	
	Dim params As Map=BANano.GetURLParams(BANano.GetCurrentUrl)
	If params.ContainsKey("id") Then
		id=params.Get("id")
	End If
	id=BANano.EncodeURI(id)
	getColumns
End Sub

Sub getColumns
	Dim url As String
	url=$"/columns?id=${id}"$
	Log("url:"&url)
	HTTPRequests.GetColumns(url)
End Sub

Public Sub loadResult(json As String)
	BANano.Window.ScrollTo(0,0)
	Dim segments As List
	Try
		segments=BANano.FromJson(json)
	Catch	
		Log(LastException)
	End Try

	If segments.Size=0 Then
		BANano.Alert("No match")
		changePageNum(currentPageNum-1)
		Return
	End If
	
	' loading layouts as array (multiple times loading the same layout)

	
	For i=0 To segments.Size - 1
		Dim result As Map
		result=segments.Get(i)
		Log(i)
		Dim detailSb As StringBuilder
		detailSb.Initialize
		Dim word As String=result.Get("word")
		Dim rowid As String=result.Get("rowid")
		Dim data As String=result.Get("highlight")
		data=data.Replace(CRLF,"<br/>")
		detailSb.Append(data)
		
		Dim Ret As Long
		Dim AllViews As Map
		
		Ret = BANano.LoadLayoutArray("#resultrow", "MultiLayout", (i=0)) ' only clear the parent if it is the first layout that is loaded
		
		' ret returns a unique number you can use to get all views
		AllViews = BANano.GetAllViewsFromLayoutArray(Me, "MultiLayout", Ret)
		Dim TitleLabel As SKLabel = AllViews.Get("titlelabel") ' always lowercase
		TitleLabel.Text = $"<a target="_blank" href="/detail?id=${id}&rowid=${rowid}">${word}</a>"$
		Dim mLabel As SKLabel = AllViews.Get("contentlabel") ' always lowercase
		mLabel.Text = detailSb.ToString
	Next
	If segments.Size>0 Then
		loadPaginator
	End If
End Sub

Sub loadCategories(json As String)
	Log(json)
	Dim columns As List
	columns=BANano.FromJson(json)
	Dim exclude As List
	exclude.Initialize
	exclude.AddAll(Array As String("word","content","raw"))
	Dim newColumns As List
	newColumns.Initialize
	For i=0 To columns.Size - 1
		If exclude.IndexOf(columns.Get(i))=-1 Then
			newColumns.Add(columns.Get(i))
		End If
	Next
	categoryNum=newColumns.Size
	For i=0 To newColumns.Size - 1
		Dim column As String=newColumns.Get(i)
		Dim Ret As Long
		Dim AllViews As Map
		Ret = BANano.LoadLayoutArray("#categoryrow", "oneCategory", (i=0)) ' only clear the parent if it is the first layout that is loaded
		Dim e As BANanoElement=BANano.GetElement("#disableradio_"&(i+1)&"chk")
		e.SetAttr("name",column)
		e.SetChecked(True)
		Dim e As BANanoElement=BANano.GetElement("#posradio_"&(i+1)&"chk")
		e.SetAttr("name",column)
		Dim e As BANanoElement=BANano.GetElement("#negradio_"&(i+1)&"chk")
		e.SetAttr("name",column)
		Dim e As BANanoElement=BANano.GetElement("#neuradio_"&(i+1)&"chk")
		e.SetAttr("name",column)
		Dim e As BANanoElement=BANano.GetElement("#categorylabel_"&(i+1))
		e.SetText(column)
	Next
End Sub

Sub getCategoryParams As String
	Dim sb As StringBuilder
	sb.Initialize
	For i=0 To categoryNum-1
		Dim value As String
		Dim disableRadio As BANanoElement=BANano.GetElement("#disableradio_"&(i+1)&"chk")
		Dim posRadio As BANanoElement=BANano.GetElement("#posradio_"&(i+1)&"chk")
		Dim negRadio As BANanoElement=BANano.GetElement("#negradio_"&(i+1)&"chk")
		Dim neuRadio As BANanoElement=BANano.GetElement("#neuradio_"&(i+1)&"chk")
		Dim categorylabel As BANanoElement=BANano.GetElement("#categorylabel_"&(i+1))
		If disableRadio.GetChecked Then
			Continue
		End If
		sb.Append("&").Append(categorylabel.GetText).Append("=")
		If posRadio.GetChecked Then
			sb.Append(posRadio.GetValue)
		End If
		If negRadio.GetChecked Then
			sb.Append(negRadio.GetValue)
		End If
		If neuRadio.GetChecked Then
			sb.Append(neuRadio.GetValue)
		End If
	Next
	Return sb.ToString
End Sub

Sub loadPaginator
	BANano.LoadLayout("#paginator", "paginator")
	addHandler
	changePageNum(currentPageNum)
End Sub

Sub addHandler
	Dim previousTxt As BANanoElement
	previousTxt=BANano.GetElement("#previouspage")
	Dim nextTxt As BANanoElement
	nextTxt=BANano.GetElement("#nextpage")
	previousTxt.HandleEvents("click", Me, "previouspage_click")
	nextTxt.HandleEvents("click", Me, "nextpage_click")
End Sub

Sub changePageNum(num As Int)
	currentPageNum=Max(1,num)
	BANano.GetElement("#pagenum").SetValue(num)
End Sub

Sub SearchButton_Click (event As BANanoEvent)
	Log("button 1 click")
	currentPageNum=1
	DoSearch
	' to prevent also raising the R1_Click event
	event.StopPropagation	
End Sub

Sub DoSearch
	Dim text As String=BANano.GetElement("#keyword").GetValue
	Log(text)
	Dim url As String
	text=BANano.EncodeURI(text)
	url=$"/get?id=${id}&page=${currentPageNum}&keyword=${text}${getCategoryParams}"$
	Log("url"&url)
	HTTPRequests.GetResults(url)
End Sub

Sub R1_Click (event As BANanoEvent)
	Log("R1 click")
End Sub

Sub SKNavigationBar1_Click (event As BANanoEvent, menu As String)
	Log("Clicked on menu: " & menu)
	Select menu
		Case "upload"
			BANano.Window.Open("/upload.html","","",False)
		Case "about"
			BANano.Window.Open("/about.html","","",False)
	End Select
	
	SKNavigationBar1.CloseDrawerMenu
End Sub

Sub PreviousPage_Click (event As BANanoEvent)
	Log(currentPageNum)
	jumpToPage(currentPageNum-1)
End Sub

Sub NextPage_Click (event As BANanoEvent)
	Log(currentPageNum)
	jumpToPage(currentPageNum+1)
End Sub

Sub jumpToPage(num As Int)
	If num<=0 Then 
		Return
	End If
	Dim startNum As Int=num-1
	changePageNum(num)
	DoSearch
End Sub

Sub PageNum_Keyup (event As BANanoEvent)
	If event.Key="Enter" Then
		jumpToPage(BANano.GetElement("#pagenum").GetValue)
	End If
End Sub

Sub Keyword_KeyUp (event As BANanoEvent)
	If event.Key="Enter" Then
		currentPageNum=1
		DoSearch
	End If
End Sub

Sub ResultRow_Click (event As BANanoEvent)
	Log("resultrow")
End Sub